// BSD 2-Clause License
//
// Copyright (c) 2020 Alasdair Armstrong
//
// All rights reserved.
//
// Redistribution and use in source and binary forms, with or without
// modification, are permitted provided that the following conditions are
// met:
//
// 1. Redistributions of source code must retain the above copyright
// notice, this list of conditions and the following disclaimer.
//
// 2. Redistributions in binary form must reproduce the above copyright
// notice, this list of conditions and the following disclaimer in the
// documentation and/or other materials provided with the distribution.
//
// THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
// "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
// LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
// A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
// HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
// SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
// LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
// DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
// THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
// (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
// OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

use std::str::FromStr;

use isla_lib::config::ISAConfig;

use crate::page_table::setup::*;
use crate::page_table::setup::AddressConstraint::*;
use crate::page_table::setup::TableConstraint::*;
use crate::page_table::setup_lexer::Tok;

grammar<'input, B>(
    isa_config: &ISAConfig<B>,
);

SetupRev: Vec<Constraint> = {
    <constraint:Constraint> ";" <mut setup:SetupRev> => {
        setup.push(constraint);
        setup
    },
    <constraint:BracedConstraint> ";"? <mut setup:SetupRev> => {
        setup.push(constraint);
        setup
    },
    <constraint:Constraint> => vec![constraint],
    => Vec::new(),
}

pub Setup: Vec<Constraint> = {
   <mut setup:SetupRev> => setup.drain(..).rev().collect(),
}

Aligned: u64 = {
    "aligned" <n:"natural"> => u64::from_str_radix(n, 10).unwrap(),
    => isa_config.symbolic_addr_stride,
}

AttrField: (String, String) = {
    <name:Id> "=" <h:Hex> => (name, h),
    <name:Id> "=" <b:Bin> => (name, b),
}

Attrs: Attrs = {
   "default" => Attrs::Default(Vec::new()),
   "code" => Attrs::Code,
   "[" <field1:AttrField> <mut fieldn:("," <AttrField>)*> "]" => {
        let mut fields = vec![field1];
        fields.append(&mut fieldn);
        Attrs::Default(fields)
    },
}

WithAttrs: Attrs = {
    "with" <attrs:Attrs> => attrs,
    "with" <s1attrs:Attrs> "and" <s2attrs:Attrs> => Attrs::Stages(Box::new(s1attrs), Box::new(s2attrs)),
    => Attrs::Default(Vec::new()),
}

Bool: bool = {
    "true" => true,
    "false" => false,
}

Level: u64 = {
    "at" "level" <n:"natural"> => u64::from_str_radix(n, 10).unwrap(),
    => 3,
}

Name: Option<String> = {
    "as" <name:Id> => Some(name),
    => None,
}

Constraint: Constraint = {
    "option" <name:Id> "=" <b:Bool> => Constraint::Option(name, b),
    <alignment:Aligned> "virtual" <addrs:Id+> => Constraint::Address(Virtual(alignment, addrs)),
    <alignment:Aligned> "intermediate" <addrs:Id+> => Constraint::Address(Intermediate(alignment, addrs)),
    <alignment:Aligned> "physical" <addrs:Id+> => Constraint::Address(Physical(alignment, addrs)),
    "identity" <exp:Exp> <attrs:WithAttrs> <level:Level> <name:Name> => Constraint::Table(IdentityMap(exp, attrs, level, name)),
    <lhs:Exp> "|->" <rhs:Exp> <attrs:WithAttrs> <level:Level> <name:Name> => Constraint::Table(MapsTo(lhs, rhs, attrs, level, name)),
    <lhs:Exp> "?->" <rhs:Exp> <attrs:WithAttrs> <level:Level> <name:Name> => Constraint::Table(MaybeMapsTo(lhs, rhs, attrs, level, name)),
    "*" <addr:Exp> "=" <exp:Exp> => Constraint::Initial(addr, exp),
    "assert" <exp:Exp> => Constraint::Address(Assertion(exp)),
    "let" <name:Id> "(" <arg1:Id> <mut argn:("," <Id>)*> ")" "=" <body:Exp> => {
        let mut args = vec![arg1];
        args.append(&mut argn);
        Constraint::Address(Function(name, args, body))
    },
    <stage:Stage> <name:Id> <addr:Exp?> => Constraint::Nested(stage, name, addr, Vec::new()),
}

Stage: Stage = {
    "s1table" => Stage::S1,
    "s2table" => Stage::S2,
}

BracedConstraint: Constraint = {
    <stage:Stage> <name:Id> <addr:Exp?> "{" <setup:Setup> "}" => Constraint::Nested(stage, name, addr, setup),
}

Exp: Exp = {
    <lhs:UnaryExp> "&&" <rhs:UnaryExp> => Exp::App("and_bool".to_string(), vec![lhs, rhs]),
    <lhs:UnaryExp> "||" <rhs:UnaryExp> => Exp::App("or_bool".to_string(), vec![lhs, rhs]),
    <lhs:UnaryExp> "&" <rhs:UnaryExp> => Exp::App("and_bits".to_string(), vec![lhs, rhs]),
    <lhs:UnaryExp> "|" <rhs:UnaryExp> => Exp::App("or_bits".to_string(), vec![lhs, rhs]),
    <lhs:UnaryExp> "^" <rhs:UnaryExp> => Exp::App("xor_bits".to_string(), vec![lhs, rhs]),
    <lhs:UnaryExp> "==" <rhs:UnaryExp> => Exp::App("eq_bits".to_string(), vec![lhs, rhs]),
    <lhs:UnaryExp> "!=" <rhs:UnaryExp> => Exp::App("neq_bits".to_string(), vec![lhs, rhs]),
    <exp:UnaryExp> => exp,
}

UnaryExp: Exp = {
    "~" <exp:AtomicExp> => Exp::App("not_bits".to_string(), vec![exp]),
    <exp:AtomicExp> => exp,
}

AtomicExp: Exp = {
    <f:Id> "(" ")" => Exp::App(f, Vec::new()),
    <f:Id> "(" <arg1:Exp> <mut argn:("," <Exp>)*> ")" => {
        let mut args = vec![arg1];
        args.append(&mut argn);
        Exp::App(f, args)
    },
    <exp:AtomicExp> "[" <from:Exp> ".." <to:Exp> "]" => Exp::App("vector_subrange".to_string(), vec![exp, from, to]),
    <id:Id> => Exp::Id(id),
    <n:"natural"> => Exp::I128(i128::from_str(n).expect("failed to parse integer")),
    <hex:Hex> => Exp::Hex(hex),
    "(" <exp:Exp> ")" => exp,
}

Id: String = <id:"identifier"> => id.to_string();
Hex: String = <b:"hex"> => b.to_string();
Bin: String = <b:"bin"> => b.to_string();

extern {
    type Location = usize;
    type Error = SetupParseError;

    enum Tok<'input> {
        "identifier" => Tok::Id(<&'input str>),
        "natural" => Tok::Nat(<&'input str>),
        "hex" => Tok::Hex(<&'input str>),
        "bin" => Tok::Bin(<&'input str>),
        "identity" => Tok::Identity,
        "s1table" => Tok::S1Table,
        "s2table" => Tok::S2Table,
        "option" => Tok::Option,
        "|->" => Tok::MapsTo,
        "?->" => Tok::MaybeMapsTo,
        "->" => Tok::Implies,
        "~" => Tok::Not,
        "true" => Tok::True,
        "false" => Tok::False,
        "assert" => Tok::Assert,
        "aligned" => Tok::Aligned,
        "virtual" => Tok::Virtual,
        "intermediate" => Tok::Intermediate,
        "physical" => Tok::Physical,
        "with" => Tok::With,
        "code" => Tok::Code,
        "default" => Tok::Default,
        "true" => Tok::True,
        "false" => Tok::False,
        "let" => Tok::Let,
        "as" => Tok::As,
        "at" => Tok::At,
        "and" => Tok::And,
        "level" => Tok::Level,
        "&&" => Tok::BooleanAnd,
        "&" => Tok::BitAnd,
        "||" => Tok::BooleanOr,
        "|" => Tok::BitOr,
        ":" => Tok::Colon,
        ";" => Tok::Semi,
        "==" => Tok::EqEq,
        "!=" => Tok::NotEq,
        "=" => Tok::Eq,
        "*" => Tok::Star,
        "^" => Tok::Caret,
        "," => Tok::Comma,
        ".." => Tok::DotDot,
        "." => Tok::Dot,
        "(" => Tok::Lparen,
        ")" => Tok::Rparen,
        "[" => Tok::Lsquare,
        "]" => Tok::Rsquare,
        "{" => Tok::Lbrace,
        "}" => Tok::Rbrace,
    }
}
